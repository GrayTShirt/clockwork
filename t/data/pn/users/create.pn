;;
;; users/create.pn
;; Pendulum Test Script
;;

SET %F "t/tmp/pn/users/passwd"
COPY %F %A
CALL &PWDB.OPEN
OK? @pwdb.open.fail

SET %E "t/tmp/pn/users/shadow"
COPY %E %A
CALL &SPDB.OPEN
OK? @spdb.open.fail

SET %A 1     ;; by username
SET %B "new"
CALL &USER.FIND
OK? @start
  PRINT "found user 'new' before we created it...\n"
  HALT

start:
PRINT "user 'new' does not exist\n"
SET %A "new" ;; username
SET %B 9001  ;; UID
SET %C 9002  ;; GID
CALL &USER.CREATE
OK? @create.fail

COPY %F %A
CALL &PWDB.WRITE
OK? @create.fail
CALL &PWDB.CLOSE
OK? @create.fail

CALL &PWDB.OPEN
OK? @pwdb.open.fail

COPY %E %A
CALL &SPDB.WRITE
OK? @create.fail
CALL &SPDB.CLOSE
OK? @create.fail

CALL &SPDB.OPEN
OK? @spdb.open.fail

SET %A 1
SET %B "new"
CALL &USER.FIND
OK? @missing

PRINT "user 'new' found, post-create\n"

CALL &USER.GET_UID
COPY %R %A
PRINT "UID is %i\n"

CALL &USER.GET_GID
COPY %R %A
PRINT "GID is %i\n"

HALT

missing:
  PRINT "unable to find 'new' user, post-create\n"
  SYSERR
  HALT

spdb.open.fail:
  PRINT "failed to open shadow file\n"
  SYSERR
  HALT

pwdb.open.fail:
  PRINT "failed to open passwd file\n"
  SYSERR
  HALT

create.fail:
  PRINT "failed to create new user\n"
  SYSERR
  HALT
