;;
;; Mesh Pendulum Standard Library
;;   mesh.pn
;;
;; author:  James Hunt <james@niftylogic.com>
;; created: 2015-01-22
;;

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
;; mesh.unhandled
;;
;; Handles an unknown or unrecognized mesh command.
;;
;; Params:
;;  (%a) The original command string
;;
fn mesh.unhandled
  print "UNRECOGNIZED mesh command: \"%[a]s\"\n"
  retv 3

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
;; mesh.show.version
;;
;; Prints the current version of Clockwork to standard output
;; and then returns 0, always.  This action cannot fail.
;;
fn mesh.show.version
  property "version" %a
  print "%[a]s\n"
  retv 0

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
;; mesh.show.acls
;;
;; Print out a list of all in-force Mesh ACL entries.
;;
fn mesh.show.acls
  show.acls
  retv 0

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
;; mesh.show.acl
;;
;; Print out the subset of Mesh ACL entries that affect the
;; named user or group.
;;
;; Parameters:
;;  (%a) Name of the user or group to limit ACL display to.
;;       Group names start with ':' (colon).
;;
fn mesh.show.acl
  show.acl %a
  retv 0

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
;; mesh.cfm
;;
;; Trigger a CFM (configuration management) run, to force
;; policy retrieval, validation and enforcement, ahead of
;; schedule.
;;
;; Returns 0 if the execution was successful, or non-zero
;; if running cogd was not possible.
;;
;; Parameters: none
;;
fn mesh.cfm
  exec "cogd -F1" %a acc %b
  print %a
  retv %b

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
;; mesh.service
;;
;; Manage System Services
;;
;; Parameters:
;;   (%a) Name of the service to manage
;;   (%b) Action to take (start/stop/reload/etc.)
;;
fn mesh.service
  streq %b "enable" jnz +1
    localsys "svc-enable %[a]s" %c
  streq %b "disable" jnz +1
    localsys "svc-disable %[a]s" %c
    localsys "svc-init %[a]s %[b]s" %c

  acc %d
  print %c
  retv %d

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
;; mesh.package
;;
;; Manage System Packages
;;
;; Parameters:
;;   (%a) Name of the package to manage
;;   (%b) Action to take (version|latest|install|remove)
;;   (%c) Optional argument (used for install)
;;
fn mesh.package
  streq %b "install" jz +1
    localsys "pkg-%[b]s %[a]s" %c
    localsys "pkg-install %[a]s %[c]s" %c

  acc %d
  print %c
  retv %d
